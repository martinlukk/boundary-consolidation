capture log close
log using 01-clean/_logs/09-merge-controls, replace text


//  Program:    09-merge-controls.do
//  Task:       Clean and merge country-level control variables with ISSP data.
//
//  Input:      pwt91.dta
//              UN_MigrantStockTotal_2019.xlsx
//              wgidataset-fixed.dta
//              KOFGI_2019_data.dta
//              x-08-issp-allwaves.dta
//
//  Output:     x-09-pennwt-cleaned.dta
//              x-10-unmig1-migstock-cleaned.dta
//              x-11-ummig2-migchg-cleaned.dta
//              x-12-wbgov-cleaned.dta
//              x-13-kofgi-cleaned.dta
//              x-15-issp_ctrls-cleaned.dta
//
//  Project:    boundary-consolidation
//  Author:     Martin Lukk / 2023-11-03 (last updated)


//  #0
//  PROGRAM SETUP  //////////////////////////////////////////////////////////
version 15
clear all
macro drop _all
set linesize 80

local date : display %tdCY-N-D date("$S_DATE", "DMY")
local tag    "09-merge-controls.do ml `date'"


//  #1
//  CLEAN GDP PER CAPITA - PENN WORLD TABLE  ////////////////////////////////
use data/input/pwt91.dta, clear

* Convert ISO3 string country codes to ISO3 numeric country codes:
kountry countrycode, from(iso3c) to(iso3n) marker
drop if MARKER == 0
replace _ISO3N_ = 643 if country == "Russian Federation"

clonevar countryV3 = _ISO3N_
label var countryV3 "Country"
notes countryV3: Clone of _ISO3N_ generated by -kountry-, containing ///
                 standard country codes for Penn WT country variable \ `tag'

* Generate GDPPC variable:
generate gdppc = (rgdpe / pop) / 1000
label var gdppc "Expenditure-side real GDP (thousands $) at chained PPPs (per capita, in 2011US$)"
notes gdppc: Derived from dividing rgdp (Expenditure-side real GDP ///
             at chained PPPs, in mil. 2011US$) variable by pop     ///
             (in millions) variable in Penn World Table data, divided by 1000 \ `tag'
notes gdppc: Variable contains "Expenditure-side real GDP at chained PPPs" ///
           (in million of US dollars), used                        ///
           "to compare relative living standards across countries and over time"; ///
           this kind of measure is generally referred to as ///
           "constant-price real GDP" as it makes a correction for  ///
           changing reference prices.
notes gdppc: Note from PWT 9.1 user guide:                           ///
           "‘Constant prices’ is a somewhat dated term in National Accounts, as the widespread adoption of chained index numbers means that data cannot be said to be in prices of a particular year. Various alternative terms are therefore used, including ‘real GDP’, the ‘quantity of GDP’ and the ‘volume of GDP’. The aim is the same, namely to have a GDP series that does not reflect changes in prices" (p. 2, fn. #2).

rename pop pop_pwt
keep countryV3 year gdppc pop_pwt
label data "Penn World Table 9.1, Real GDPPC Variable"
datasignature set, reset

save data/output/_temp/x-09-pennwt-cleaned.dta, replace


//  #2
//  CLEAN MIGRANT STOCK - UN  ///////////////////////////////////////////////

/* Import table section "International migrant stock as a percentage of the ///
   total population (both sexes)" only: */
import excel data/input/UN_MigrantStockTotal_2019.xlsx  ///
    , sheet("Table 3") cellrange(A15:L299) firstrow clear

drop Sortorder Majorarearegioncountryora Notes Typeofdataa
* Drop non-country rows, except first row:
drop if Code == . & _n != 1
* Replace ".." with missing ".":
foreach v of varlist Internationalmigrantstockasa G H I {
    replace `v' = "." if `v' == ".."
}
destring *, replace

rename International~a migstock_1990
* Extract var year (stored in first row, Code == m) and rename:
foreach v of varlist G - L {
    sum `v' if missing(Code), meanonly
    local `v'year = r(mean)
    rename `v' migstock_``v'year'
}

rename Code countryV3
label var countryV3 "Country"
drop if missing(countryV3)
duplicates drop // Drop duplicate Oceania entry

* Separate migstock_[year] variable into migstock and year (reshape long):
reshape long migstock_, i(countryV3) j(year)
drop if missing(migstock_)

rename migstock_ migstock
label var migstock "Intl. Migrant Stock (percentage of total pop., both sexes)"
notes migstock: Variable derived from originally wide-shaped UN        ///
                Migrant Stock 2019 data set \ `tag'

* Match years with ISSP survey waves:
gen wave = .
replace wave = 1 if year == 1995
replace wave = 2 if year == 2005
replace wave = 3 if year == 2015

* Swiss and Russian ISSP surveys occurred in 2002 and 2012, respectively;  ///
  these are matched with 2000 and 2010 UN data (the closest years):
recode  wave (2 = .) if countryV3 == 756 & year == 2005
replace wave    = 2  if countryV3 == 756 & year == 2000
recode  wave (3 = .) if countryV3 == 643 & year == 2015
replace wave    = 3  if countryV3 == 643 & year == 2010

keep if !missing(wave)
drop year
sort countryV3 wave

label data "UN Migration Stock Data, Migrant Stock Variable"
datasignature set, reset
save data/output/_temp/x-10-unmig1-migstock-cleaned.dta, replace


//  #3
//  CLEAN DEMOCRATIC GOVERNANCE SCALE - WORLD BANK  /////////////////////////
use data/input/wgidataset-fixed.dta, replace

* Convert ISO3 string country codes to ISO3 numeric country codes:
kountry code, from(iso3c) to(iso3n) marker
replace _ISO3N_ = 643 if countryname == "Russian Federation"
replace _ISO3N_ = 642 if countryname == "Romania"
drop if MARKER == 0 | missing(_ISO3N_)

clonevar countryV3 = _ISO3N_
label var countryV3 "Country"
notes countryV3: Clone of _ISO3N_ generated by -kountry-, containing ///
                 standard country codes for country variable \ `tag'

* Store variable names for all 6 WB governance indicators: Voice and      ///
  Accountability, Political Stability and Absence of Violence, Government  ///
  Effectiveness, Regulatory Quality, Rule of Law, Control of Corruption:
local gov_all vae pve gee rqe rle cce

alpha `gov_all', item gen(governS)
di "Alpha = " round(r(alpha),.001)

label var governS "Democratic Governance Scale (Std.)"
notes governS: Democratic governance scale based on 6 items in World  ///
               Bank Governance Indicators data set (Voice and         ///
               Accountability, Political Stability and Absence of     ///
               Violence, Government Effectiveness, Regulatory         ///
               Quality, Rule of Law, Control of Corruption);          ///
               standardized summative scale (Alpha = .963) \ `tag'

keep countryV3 year governS

label data "World Bank Governance Indicators, 6-Item Scale Variable"
datasignature set, reset
save data/output/_temp/x-12-wbgov-cleaned.dta, replace


//  #4
//  CLEAN GLOBALIZATION SCALE - KOF GLOBALIZATION INDEX  ////////////////////
use data/input/KOFGI_2019_data.dta, clear

* Convert ISO3 string country codes to ISO3 numeric country codes:
kountry code, from(iso3c) to(iso3n) marker
drop if MARKER == 0
replace _ISO3N_ = 643 if country == "Russian Federation"
drop if missing(_ISO3N_)

clonevar countryV3 = _ISO3N_
label var countryV3 "Country"
notes countryV3: Clone of _ISO3N_ generated by -kountry-, containing ///
                 standard country codes \ `tag'

rename KOFTrGI kof_trade
keep countryV3 year kof_trade

label data "KOF Globalization Index 2019"
datasignature set, reset
save data/output/_temp/x-13-kofgi-cleaned.dta, replace


//  #5
//  MERGE CONTROL VARIABLES WITH ISSP  //////////////////////////////////////

//  #5.1 - Merge GDPPC
use data/output/_temp/x-08-issp-allwaves.dta, clear

merge m:1 countryV3 year                                                 ///
    using data/output/_temp/x-09-pennwt-cleaned.dta
* Drop PWT rows (country, year, GDP) for which there is no survey data:
drop if _merge == 2
drop _merge

//  #5.2 - Merge Migrant Stock
merge m:1 countryV3 wave using data/output/_temp/x-10-unmig1-migstock-cleaned.dta
* Unsuccessful merges only for Taiwan and for countries w/out survey data:
assert oecdI != 1 if _merge == 1
assert missing(id) if _merge == 2
drop if _merge == 2
drop _merge

notes migstock: NOTE: UN migration data is available at 5-year        ///
                intervals (1990, 1995, 2000, 2005, 2010, 2015) while  ///
                ISSP wave occur at or near 1995, 2003, and 2013;      ///
                years in UN migration data were                       ///
                assigned to particular waves (1-3) so that each ISSP  ///
                national survey was matched to the nearest available  ///
                UN migration data year (e.g. 94-96 to 95 data, 03-05  ///
                to 05 data, 12-15 to 15 data; cases where ISSP survey ///
                was in 2002 (Switzerland) or 2012 (Russia) were       ///
                matched with the previous (but closest) UN migration  ///
                data (from 2000 and 2010, respectively) \ `tag'

//  #5.3 - Merge Democratic Governance Scale
merge m:1 countryV3 year using data/output/_temp/x-12-wbgov-cleaned.dta
assert missing(id) if _merge == 2

* Loop that recodes missing values for governS in 1995 ISSP survey to  ///
  the first available governS value for that country (1996):
levelsof countryV3 if _merge == 1 & year == 1995, local(misscntrys)
foreach cntry of local misscntrys {
    * di "COUNTRY: `cntry'"
    qui sum year    if _merge == 2 & countryV3 == `cntry'
    * di "FIRST governS YEAR: "r(min)
    qui sum governS if _merge == 2 & countryV3 == `cntry' & year == r(min)
    * di "governS FIRST VALUE: "r(mean)
    * di _newline
    replace governS = r(mean)                                           ///
        if countryV3 == `cntry' & year == 1995 & _merge == 1
    }
* Replace missing Slovenia (1994) governS value with value for Slovenia's  ///
  earliest recorded year, 1996:
qui sum year if _merge == 2 & countryV3 == 705
qui sum governS if _merge == 2 & countryV3 == 705 & year == r(min)
replace governS = r(mean)                                             ///
    if countryV3 == 705 & year == 1994 & _merge == 1
notes governS: NOTE: Earliest available data for WB governance indicators ///
               is for year 1996; ISSP survey data from years 1994 and 1995 ///
               is matched with the the governS scale value for 1996 for    ///
               each country \ `tag'

drop if _merge == 2
drop _merge

//  #5.4 - Merge KOF Globalization Index
merge m:1 countryV3 year using data/output/_temp/x-13-kofgi-cleaned.dta
drop if _merge == 2
drop _merge


//  #6
//  SAVE NEW DATA SET  //////////////////////////////////////////////////////
order gdppc governS migstock kof_trade ///
      , after(weight)

gen DEMOGRAPHICS_ = .
order DEMOGRAPHICS_, after(id)
gen NATIONALISM_ = .
order NATIONALISM_, before(impbornP)
gen COUNTRY_LVL_ = .
order COUNTRY_LVL_, after(weight)
gen OTHER_ = .
order OTHER_, before(hompop)
order weight, after(OTHER_)

label data "ISSP Nat. ID Surveys (1-3) Merged with Country-lvl Controls \ `date'"
compress
datasignature set, reset
save data/output/_temp/x-15-issp_ctrls-cleaned.dta, replace


log close
exit
